apply plugin: 'com.android.library'
apply plugin: 'com.kageiit.lintrules'

android {
  compileSdkVersion rootProject.ext.compileSdkVersion
  buildToolsVersion rootProject.ext.buildToolsVersion

  defaultConfig {
    minSdkVersion rootProject.ext.minSdkVersion

    consumerProguardFiles 'proguard-rules.txt'
  }

  lintOptions {
    textReport true
    textOutput 'stdout'
  }
}

dependencies {
  compile project(':butterknife-annotations')
  compile deps.supportAnnotations
  compile deps.supportCompat
}

def getRuntimeJar() {
  try {
    final File javaBase = new File(System.getProperty("java.home")).getCanonicalFile();
    File runtimeJar = new File(javaBase, "lib/rt.jar");
    if (runtimeJar.exists()) {
      return runtimeJar;
    }
    runtimeJar = new File(javaBase, "jre/lib/rt.jar");
    return runtimeJar.exists() ? runtimeJar : null;
  } catch (IOException e) {
    throw new RuntimeException(e);
  }
}

apply from: rootProject.file('gradle/gradle-mvn-push.gradle')
